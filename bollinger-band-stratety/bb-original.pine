//@version=4
strategy(title="Bollinger Bands Breakout",initial_capital=1000,
     currency=currency.USD, default_qty_type=strategy.cash,
     default_qty_value=1000, commission_type=strategy.commission.cash_per_order, 
     commission_value=1.0, pyramiding=1, overlay=true)

// ========  Input Options

// Average period (width of the simple moving average window, in bars)
bolLength = input(title="Bollinger Bands Length", type=input.integer, defval=20)

// Standard deviation offset from the SMA line
sdOffset = input(title="StDev Offset", type=input.float, defval=2, minval=0.1, step=0.25)

// Target price
TP = input(title="Price Source", type=input.source, defval=hlc3)

// The starting date of trading
startMonth = input(title="Start Month", type=input.integer, defval=1)
startYear = input(title="Start Year", type=input.integer, defval=2021)

// =========  Compute values
MID = sma(TP, bolLength)
sdPrice = stdev(TP, bolLength)

BOLU = MID + (sdOffset * sdPrice)
BOLD = MID - (sdOffset * sdPrice)

orderSize = floor(0.5 * strategy.equity / close)

// ========== Plot Bollinger Bands
plot(series=BOLU, color=color.green, linewidth=2)
plot(series=MID, color=color.black)
plot(series=BOLD, color=color.red, linewidth=2)

buySignal = false
sellSignal = false

buyBoundary = MID + (sdOffset * 0.4 * sdPrice)
sellBoundary = MID + (sdOffset * 0.8 * sdPrice)
stopLoss = 0.8
stockValue = open

if (month >= startMonth and year == startYear) or year > startYear
    buySignal := crossover(stockValue, buyBoundary)
    sellSignal := stockValue < stopLoss * valuewhen(buySignal, stockValue, 0) or crossunder(stockValue, sellBoundary)

    strategy.entry(id="Long", long=true, qty=orderSize, when=buySignal)
    strategy.close(id="Long", when=sellSignal)

